apply plugin: "com.github.lburgazzoli.karaf"

apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

configurations {
    dist
    [dist].forEach {
        // Exclude packages that might interfere with Karaf's logging
        it.exclude group: 'org.slf4j'
        it.exclude group: 'commons-logging'
        // Exclude javax.servlet so that it doesn't interfere with Karaf's setup
        it.exclude group: 'javax'
        it.exclude group: 'javax.servlet'
        // Exclude other packages that would interfere with Karaf's setup
        it.exclude group: 'org.apache.karaf'
        it.exclude group: 'org.apache.karaf.jaas'
        it.exclude group: 'org.apache.karaf.shell'
        it.exclude group: 'org.apache.karaf.config'
        it.exclude group: 'org.apache.felix'
        it.exclude group: 'org.eclipse.jetty'
    }
}

dependencies {
    compile project(':cicomponents-api')
    compile project(':cicomponents-git-api')
    compile project(':cicomponents-github-api')
    compile project(':cicomponents-badges-api')

    compile 'org.gradle:gradle-tooling-api:3.1'
    dist    'org.gradle:gradle-tooling-api:3.1'

    compile 'javax.servlet:javax.servlet-api:3.1.0'
}


karaf {
    features {
        xsdVersion = '1.3.0'
        repository "mvn:org.apache.karaf.features/standard/4.0.8/xml/features"


        feature {
            name = 'cicomponents-ci'
            includeProject = true

            configurations 'dist'
        }

        outputFile = new File(
                "${project.buildDir}/karaf/features",
                "${name}-${version}-features.xml"
        )
    }

    kar {}
}


publishing {
    publications {
        Publication(MavenPublication) {
            groupId project.group
            artifactId project.name
            artifact("${project.buildDir}/karaf/features/${project.name}-${project.version}-features.xml") {
                classifier "features"
            }
        }
    }
}
publishToMavenLocal.dependsOn generateFeatures